Program                     ::=
        SourceElement $
;

SourceElement               ::=
        Statement+
;

Statement               ::=
        CompoundStatement
    |   CheckAmongStatement
    |   ConditionalStatement
    |   DeclarationStatement
    |   EmptyStatement
    |   ExpressionStatement
    |   IterativeStatement
    |   JumpStatement
    |   LabelledStatement
;

CompoundStatement       ::=
        "{" SourceElements "}"
;

LabelledStatement       ::=
    |   "isit" "Identifier" ":"
;

Literal                 ::=
        BooleanLiteral
    |   NumericLiteral
    |   StringLiteral
;

BooleanLiteral          ::=
        "true"
    |   "false"
;

Lambda                  ::=
        "@" [ "(" [ ParameterList ] ")" [ "->" TypeSpecifier ] ] CompoundStatement
;

NumericLiteral          ::=
        "NumericLiteral"
;

StringLiteral           ::=
        "StringLiteral"
;

CheckAmongStatement     ::=
        "check" Expression "among" "{" LabelledStatement+ "}"
;

ConditionalStatement    ::=
        "if" "(" Expression [ "among" Expression ] ")" Statement [ "else" Statement ]
;

IterativeStatement      ::=
        "do" Statement "while"  "(" Expression ")" ";"
    |   "while"                 "(" Expression ")" Statement
    |   "for" "(" ForStatementHelper
;

ForStatementHelper      ::=
        DeclarationStatement [ Expression ] ";" [ Expression ] ";" [ Expression ] ")" Statement
    |   Expression ":" Expression ")" Statement
;

EmptyStatement          ::=
        ";"
;

DeclarationStatement    ::=
        ClassDeclaration
    |   EnumDeclaration
    |   VariableDeclaration
    |   FunctionDeclaration
    |   NamespaceDeclaration
;

VariableDeclaration     ::=
        "var" "Identifier" [ ":" TypeSpecifier ] [ Initializer ] [ DeclarationList ] ";"
;

TypeSpecifier           ::=
        Type [ "->" TypeSpecifier ]
    |   "decltype" "(" Expression ")"
    |   "(" [ TypeSpecifierList ] ")" "->" TypeSpecifier
;

TypeSpecifierList                ::=
        TypeSpecifier [ "," TypeSpecifierList ]
;

Type                    ::=
        "Identifier" [ GenericType ] [ QualifiedType ] [ RestType ]
;

QualifiedType           ::=
        "::" Identifier
;

GenericType             ::=
       "<" TypeSpecifierList ">"
;

RestType                ::=
        "[" "NumericLiteral [ ArrayElemList ] "]"
    |   "*" RestType
;

ArrayElemList           ::=
        "," "NumericLiteral" [ ArrayElemList ]
;

TypeQualifier           ::=
        "const"
;

DeclarationList ::=
        ","  Identifier [ Initializer ] [ DeclarationList ]
;

Initalizer      ::=
        "=" AssignmentExpression
;

NamespaceDeclaration    ::=
        "namespace" [ "Identfier" ] CompoundStatement
;

EnumDeclaration         ::=
        "enum" "Identifier" "{" EnumeratorList "}" ";"
;

EnumeratorList          ::=
        Enumerator [ "," EnumeratorList ]
;

Enumerator              ::=
        "Identifier" [ "=" NumericLiteral ]
;

ClassDeclaration        ::=
        "class" "Identifier" [ GenericType ] [ BaseClasses ] "{" ClassBody "}" ";"
;

OptionalBaseClasses    ::=
        "extends" TypeSpecifierList
;

ClassBody               ::=
        [ OptionalAccessSpecifier ] ClassBodyList
;

OptionalAccessSpecifier ::=
        "private"
    |   "protected"
    |   "public"
;

ClassBodyList           ::=
        DeclarationStatement
;

FunctionDeclaration         ::=
        "function" [ FunctionSpecifier ] "Identifier" [ GenericType ] "(" [ ParameterList ] ")" [ "->" TypeSpecifier ] CompoundStatement
    |   "implement" "operator" AssignmentOperator "for" "(" ParameterList ")" [ "->" TypeSpecifier ] CompoundStatement
;

FunctionSpecifier       ::=
        "Identifier" [ "," Identifier" ]*
;

Parameter               ::=
        [ "var" ] "Identifier" ":" TypeSpecifier [ ParameterList ]
;

ParameterList           ::=
        "," Parameter [ ParameterList ]
;

ExpressionStatement     ::=
        Expression
;

Expression              ::=
        AssignmentExpression [ "," Expression ]
;

AssignmentExpression    ::=
        ConditionalExpression
    |   PostfixExpression AssignmentOperator AssignmentExpression
;

AssignmentOperator      ::=
        =
    |   *=
    |   /=
    |   %=
    |   +=
    |   -=
    |   <<=
    |   >>=
    |   &=
    |   ^=
    |   |=
;

ConditionalExpression   ::=
        LogicalOrExpression
    |   LogicalOrExpression [ "?" Expression ":" ConditionalExpression ]
;

LogicalOrExpression     ::=
        LogicalAndExpression
    |   LogicalOrExpression "||" LogicalAndExpression
;
    
LogicalAndExpression    ::=
        InclusiveOrExpression
    |   LogicalAndExpression "&&" InclusiveOrExpression
    
InclusiveOrExpression   ::=
        ExclusiveOrExpression
    |   InclusiveOrExpression "|" ExclusiveOrExpression
;
    
ExclusiveOrExpression   ::=
        AndExpression
    |   ExclusiveOrExpression "^" AndExpression
;

AndExpression           ::=
        EqualityExpression
    |   AndExpression "&" EqualityExpression
;

EqualityExpression      ::=
        RelationalExpression
    |   EqualityExpression "==" RelationalExpression
    |   EqualityExpression "!=" RelationalExpression
;

RelationalExpression    ::=
        ShiftExpression
    |   RelationalExpression "<" ShiftExpression
    |   RelationalExpression ">" ShiftExpression
    |   RelationalExpression "<=" ShiftExpression
    |   RelationalExpression ">=" ShiftExpression
;

ShiftExpression         ::=
        AdditiveExpression
    |   ShiftExpression "<<" AdditiveExpression
    |   ShiftExpression ">>" AdditiveExpression
;

AdditiveExpression      ::=
        MultiplicativeExpression
    |   AdditiveExpression "+" MultiplicativeExpression
    |   AdditiveExpression "-" MultiplicativeExpression
;

MultiplicativeExpression ::=
        UnaryExpression
    |   MultiplicativeExpression "*" UnaryExpression
    |   MultiplicativeExpression "/" UnaryExpression
    |   MultiplicativeExpression "%" UnaryExpression
;

UnaryExpression          ::=
        PostfixExpression
    |   "++" UnaryExpression
    |   "--" UnaryExpression
    |   "typeof" UnaryExpression
    |   UnaryOperator UnaryExpression
;

PrimaryExpression       ::=
    |   "Identifier"
    |   ArrayLiteral
    |   Literal
    |   Lambda
    |   "(" Expression ")"
;

PostfixExpression       ::=
        PrimaryExpression [ PostfixExpressionHelper ]
;

PostfixExpressionHelper ::=
    |   "[" Expression "]" PostfixExpressionHelper
    |   "(" [ Expression ] ")" PostfixExpressionHelper
    |   "." "Identifier" PostfixExpressionHelper
    |   "++" PostfixExpressionHelper
    |   "--" PostfixExpressionHelper
    |   Epsilon
;

UnaryOperator           ::=
    |   -
    |   ~
    |   !
;

ArrayLiteral            ::=
    |   "[" OptionalElementList "]"
;

OptionalElementList     ::=
        AssignmentExpression [ "," OptionalElementList ]
;

ObjectLiteral           ::=
        "{" [ PropertyNameAndValueList ] "}"
;

PropertyNameAndValueList ::=
        PropertyAssignment [ "," PropertyNameAndValueList ]
;

PropertyAssignment      ::=
        PropertyName ":" AssignmentExpression
;

PropertyName            ::=
        "Identifier"
    |   StringLiteral
    |   NumericLiteral
;

PropertySetParameterList ::=
        "Identifier"
;

JumpStatement           ::=
        "continue" ";"
    |   "leave" ";"
    |   "return" Expression ";"
;
